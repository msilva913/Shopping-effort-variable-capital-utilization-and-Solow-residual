

// define variables

var Y           ${Y}$ (long_name='output')
    C           ${C}$ (long_name='consumption')
    I           ${I}$ (long_name = 'investment')
    I_C         ${I_C}$ (long_name = 'investment:C')
    I_I         ${I_I}$ (long_name = 'investment:I')
    K           ${K}$ (long_name='Capital')
    K_C         ${K_C}$ (long_name='Capital:C')
    K_I         ${K_I}$ (long_name='Capital:I')
    N           ${N}$   (long_name='Hours')
    N_C         ${N_C}$ (long_name='Hours:C')
    N_I         ${N_I}$ (long_name='Hours:I')
    N_comp           ${N}$ (long_name= 'Labor CES aggregate')

    Z_C         ${Z_C}$ (long_name='Tech:C')
    u_ZI
    Z_I         ${Z_I}$ (long_name='Tech:I')
    theta_N     ${\theta_N}$ (long_name='Labor disutility')
    theta_D     ${\theta_D}$ (long_name='Shopping disutility')
    theta_I     ${\theta_I}$ (long_name ='Relative shopping disutility')
    theta_b     ${\theta_b}$ (long_name='Discount factor shock')
    mu_C        ${\mu_C}$
    mu_I        $\mu_I}$
    R_C         ${R_C}$ (long_name='Capital rental rate:C')
    R_I         ${R_I}$ (long_name='Capital rental rate:I')
    W_C           ${W_C}$ (long_name='Real wage:C')
    W_I           ${W_I}$ (long_name='Real wage:C')

    h_C         ${h_C}$ (long_name= 'Capital utilization rate:C')
    h_I         ${h_I}$ (long_name= 'Capital utilization rate:I')

    delta_C    ${\delta_C}$ (long_name= 'Capital depreciation rate:C')
    delta_I    ${\delta_I}$ (long_name= 'Capital depreciation rate:I')

    delta_C_pr    ${\delta_C}$ (long_name= 'Capital depreciation rate derivative:C')
    delta_I_pr    ${\delta_I}$ (long_name= 'Capital depreciation rate derivative:I')
    Sc            $S$ (long_name = 'Investment adjustment cost:C')
    Si            $S$ (long_name = 'Investment adjustment cost:I')
    Sc_pr         $S_pr$ (long_name = 'Derivative investment adjustment cost:C')
    Si_pr         $S_pr$ (long_name = 'Derivative investment adjustment cost:I')

    D           ${D}$ (long_name='Shopping effort')
    D_C           ${D}$ (long_name='Shopping effort:C')
    D_I           ${D}$ (long_name='Shopping effort:I')

    Gam        ${\Gamma}$ (long_name = 'Composite utility term')
    zeta       ${\zeta}$ (long_name = 'Wealth effects parameter')

    p_I        ${p_I}$ (long_name = 'Relative investment price')
    Q_C          ${Q}$ (long_name = 'Relative price of capital:C')
    Q_I          ${Q}$ (long_name = 'Relative price of capital:I')
    x_C          ${x}$ (long_name = 'Growth rate of investment:C')
    x_I          ${x}$ (long_name = 'Growth rate of investment:I')


    log_SR         (long_name='Solow residual')
    
    util       ${util}$ (long_name = 'Capacity utilization')
    util_ND    ${util_{ND}}$ (long_name = 'Capacity utilization:ND')
    util_D     ${util_D}$ (long_name = 'Capacity utilization:D')

    g          ${g}$ (long_name = 'Growth rate of stochastic trend')
    
    log_Y
    log_C
    log_I
    log_N
    log_NC
    log_NI
    log_K
    log_Y_N
    log_D
    log_p_I
    log_util
    log_util_ND
    log_util_D
    
    C_obs
    I_obs
    Y_obs
    Y_N_obs
    p_I_obs
    N_obs
    NC_obs
    NI_obs
    util_ND_obs
    util_D_obs
    util_obs
    D_obs
    K_obs
    SR_obs
  
    ;

varexo e_g ${e_g}$ (long_name= 'Labor-augmenting-technology growth shock')
       e_Z ${e_Z}$ (long_name= 'TFP shock')
       e_ZI ${e_{ZI}}$ (long_name= 'Investment-specific tech shock')
       
       e_N ${e_N}$ (long_name= 'Labor supply shock')
       e_D ${e_D}$ (long_name = 'Shopping disutility shock')
       e_DI ${e_DI}$ (long_name = 'Relative investment shopping disutility shock')
       e_b ${e_b}$ (long_name = 'Discount factor shock')

       e_muC ${e_{muC}}$ (long_name = 'Wage markup shock: C')
       e_muI ${e_{muI}}$ (long_name = 'Wage markup shock: I')

       % News shocks
       e_g_news ${e_{g,-4}}$ (long_name= 'Labor-augmenting-technology growth shock: news')
       e_Z_news ${e_{Z,-4}}$ (long_name= 'TFP shock: news')
       e_ZI_news ${e_{ZI,-4}}$ (long_name= 'Investment-specific tech shock: news')

       e_D_nes ${e_{D,4}}$ (long_name = 'Shopping disutility shock: news')
       e_DI ${e_{DI,-4}}$ (long_name = 'Relative investment shopping disutility shock: news')

       e_muC ${e_{muC,-4}}$ (long_name = 'Wage markup shock: C: news')
       e_muI ${e_{muI,-4}}$ (long_name = 'Wage markup shock: I: news')
    ;
    
parameters 
    gam    ${\gamma}$   (long_name= 'Risk aversion')
    gam_max ${\gamma_{max}}$ (long_name= 'Upper bound on risk aversion')
    beta  ${\beta}$    (long_name='Discount factor')
    g_bar  ${\overline{g}}$ (long_name = 'Quarterly trend growth rate')
    nu     $\nu$       (long_name = 'Frisch elasticity')
    sigma_s $\sigma_s$ (long_name = 'wealth effect on labor supply')
    mu_ss    $\mu_{ss}$ (long_name = 'steady-state wage markup')

    sigma_ac ${\sigma_{ac}}$ (long_name = 'Inverse elasticity of marginal utilization cost wrt rental rate:C')
    sigma_ai ${\sigma_{ai}}$ (long_name = 'Inverse elasticity of marginal utilization cost wrt rental rate:I')
    Psi_C ${\Psi_C}$ (long_name = 'Investment adjustment cost parameter:C')
    Psi_I ${\Psi_I}$ (long_name = 'Investment adjustment cost parameter:I')
   
    I_Y    ${I_Y}$   (long_name = 'Investment-output ratio')
    K_Y    ${K_Y}$   (long_name = 'Capital-output ratio (quarterly)')
    labor_share    $(labor share)$   (long_name = 'Labor share')
    nu_R   ${\nu_R}$ (long_name = 'Fixed cost share')
    ha     ${ha}$ (long_name = 'Habit persistence')

    //phi  ${\phi}$ (long_name = 'Shopping matching function elasticity')
    m    ${m}$    (long_name = 'Ratio of price dispersion to consumption dispersion')
    eta  ${\eta}$ (long_name = 'Shopping disutility')
    Psi  ${\Psi}$ (long_name = 'Matching utilization')

    theta ${\theta}$ (long_name = 'Inverse intersectoral elasticity of labor supply')

    rho_g    ${\rho_g}$  (long_name='persistence TFP growth shock')
    rho_Z   ${\rho_Z}$  (long_name='persistence TFP shock')
    rho_ZI    ${\rho_{ZI}}$  (long_name='persistence I-specific shock')
    rho_N    ${\rho_N}$  (long_name='persistence labor supply shock')
    rho_D    ${\rho_D}$  (long_name='persistence shopping effort shock')
    rho_DI   ${\rho_{DI}}$ (long_name='persistence relative shopping effort shock')
    rho_b    ${\rho_b}$  (long_name='persistence discount factor shock')
    rho_muC  ${\rho_{muC}}$ (long_name='persistence wage markup shock:C')
    rho_muI  ${\rho_{muI}}$ (long_name='persistence wage markup shock:I')

    p_I_ss
    N_ss
    ;

%----------------------------------------------------------------
% set parameter values 
%----------------------------------------------------------------
gam = 2.0; % risk aversion
beta = 0.99; % discount factor
//r_ann = 0.04; % annual interest rate 
g_bar = 0.0045; % quarterly growth rate
//gam_max = (1/4)*log(1+r_ann)/g_bar;
nu = 0.72; % Frisch
sigma_s = 0.5;
ha = 0.1;
mu_ss = 1.15; % steady-state wage markup

sigma_ac = 0.32; % inverse of elasticity of capital utilization wrt rental rate
sigma_ai = 0.32;
Psi_C = 1.5;
Psi_I = 1.5;

I_Y = 0.20;
K_Y = 11;
labor_share = 0.67;
nu_R = 0.2; % share of fixed costs in output

m = 0.286;
eta = 0.20;
//phi = 0.32;
Psi = 0.81;

theta = 0.5;

p_I_ss = 1.0;
N_ss = 0.30;

rho_g = 0.1;
rho_Z = 0.9;
rho_ZI = 0.9;
rho_N = 0.9;
rho_D = 0.9;
rho_DI = 0.9;
rho_b = 0.9;
rho_muC = 0.9;
rho_muI = 0.9;

%----------------------------------------------------------------
% enter model equations
%----------------------------------------------------------------

model;
% Dependent parameters
#I_K = I_Y/K_Y;
#delta = I_K + 1 - exp(g_bar);
//#r = (1+r_ann)^(1/4) - 1.0;
//#beta=(1/(1+r))*(exp(g_bar))^(gam);
#r = exp(g_bar)^(gam)/beta - 1;

# sigma_b = r + delta;
#phi = (eta+1)*m/(1+eta*m);

#alpha_K = (r+delta)*K_Y/(1+nu_R);
#alpha_N = (1-phi)*labor_share/(1+nu_R);


#D_ss = phi^(eta/(1+eta));
#D_C_ss = (1-I_Y)*D_ss;
#D_I_ss = I_Y*D_ss;
#A_C = Psi/D_C_ss^phi;
#A_I = Psi/D_I_ss^phi;

#I_ss = I_Y;
#C_ss = 1 - I_ss;
#K_ss = K_Y*exp(g_bar);
#K_I_ss = I_Y*K_ss;
#K_C_ss = (1-I_Y)*K_ss;
#N_I_ss = I_Y*N_ss;
#N_C_ss = (1-I_Y)*N_ss;
#omega = N_C_ss/N_ss;

#W_ss = labor_share/N_ss;

#nu_C = nu_R*C_ss/Psi;
#nu_I = nu_R*I_ss/Psi;

#Z_C_ss = (C_ss/(Psi) + nu_C)/(exp(g_bar)^(-alpha_K)*K_C_ss^(alpha_K)*N_C_ss^(alpha_N));
#Z_I_ss = (I_ss/Psi+nu_I)/(exp(g_bar)^(-alpha_K)*K_I_ss^(alpha_K)*N_I_ss^(alpha_N));

#zeta_ss = C_ss*(1-ha) - D_ss^(1+1/eta)/(1+1/eta);
#theta_N_ss = (1-phi)*W_ss/(N_ss^(1/nu)*zeta_ss*mu_ss);

[name = 'Labor composite']
N_comp = (omega^(-theta)*N_C^(1+theta) + (1-omega)^(-theta)*N_I^(1+theta))^(1/(1+theta));

[name='Labor leisure:C']
theta_N_ss*exp(theta_N)*(N_comp)^(1/nu)*(N_C/N_comp)^theta*omega^(-theta) = (1-phi)*W_C/(mu_ss*exp(mu_C)*zeta);

[name='Labor leisure:I']
theta_N_ss*exp(theta_N)*(N_comp)^(1/nu)*(N_I/N_comp)^theta*(1-omega)^(-theta)  = (1-phi)*W_I/(mu_ss*exp(mu_I)*zeta);

[name='Shopping:C']
exp(theta_D)*D^(1/eta) = phi*C/D_C;

[name = 'Shopping:I']
//exp(theta_D)*D^(1/eta) = phi*p_I*I/D_I;
exp(theta_D)*D^(1/eta)*exp(theta_I) = phi*p_I*I/D_I;

[name = 'Composite utility term']
Gam = C-ha*C(-1) - exp(theta_D)*D^(1+1/eta)/(1+1/eta) - theta_N_ss*exp(theta_N)*N_comp^(1+1/nu)/(1+1/nu)*zeta;

[name = 'Law of motion of wealth effects variable']
zeta =  (C-ha*C(-1) - exp(theta_D)*D^(1+1/eta)/(1+1/eta))^(sigma_s)*zeta(-1)^(1-sigma_s);

[name = 'Investment adjustment cost function:C']
Sc =Psi_C/2*(x_C-exp(g_bar))^2;

[name = 'Investment adjustment cost function:I']
Si =Psi_I/2*(x_I-exp(g_bar))^2;

[name = 'Investment adjustment cost function: derivative C']
Sc_pr = Psi_C*(x_C-exp(g_bar));

[name = 'Investment adjustment cost function: derivative I']
Si_pr = Psi_I*(x_I-exp(g_bar));

[name = 'Investment growth:C']
x_C = I_C/I_C(-1)*exp(g);

[name = 'Investment growth:I']
x_I = I_I/I_I(-1)*exp(g);

[name = 'Tobins Q: C']
p_I/(1-phi) = Q_C*(1-Sc_pr*x_C-Sc) + 
    beta*exp(theta_b)*(Gam(+1)/Gam)^(-gam)*exp(g(+1))^(-gam)*Q_C(+1)*Sc_pr(+1)*x_C(+1)^2;

[name = 'Tobins Q: I']
p_I/(1-phi) = Q_I*(1-Si_pr*x_I-Si) + 
    beta*exp(theta_b)*(Gam(+1)/Gam)^(-gam)*exp(g(+1))^(-gam)*Q_I(+1)*Si_pr(+1)*x_I(+1)^2;

[name= 'Euler equation: C']
Q_C = beta*exp(theta_b)*(Gam(+1)/Gam)^(-gam)*exp(g(+1))^(-gam)*(R_C(+1)*h_C(+1) + (1-delta_C(+1))*Q_C(+1));

[name = 'Euler equation: I']
Q_I = beta*exp(theta_b)*(Gam(+1)/Gam)^(-gam)*exp(g(+1))^(-gam)*(R_I(+1)*h_I(+1) + (1-delta_I(+1))*Q_I(+1));


[name = 'Utilization: C']
delta_C_pr*Q_C = R_C;

[name = 'Utilization: I']
delta_I_pr*Q_I = R_I;

[name = 'Depreciation rate: C']
delta_C = delta + sigma_b*(h_C-1) + sigma_ac*sigma_b/2*(h_C-1)^2;

[name = 'Depreciation rate: I']
delta_I = delta + sigma_b*(h_I-1) + sigma_ai*sigma_b/2*(h_I-1)^2;

[name = 'Depreciation rate derivative: C']
delta_C_pr = sigma_b + sigma_ac*sigma_b*(h_C-1);

[name = 'Depreciation rate derivative: I']
delta_I_pr = sigma_b + sigma_ai*sigma_b*(h_I-1);

[name = 'Consumption production']
C = A_C*(D_C)^phi*(Z_C_ss*exp(g)^(-alpha_K)*exp(Z_C)*(h_C*K_C(-1))^alpha_K*(N_C)^alpha_N-nu_C);

[name = 'Investment production']
I = A_I*(D_I)^phi*(Z_I_ss*exp(g)^(-alpha_K)*exp(Z_I)*(h_I*K_I(-1))^alpha_K*(N_I)^alpha_N-nu_I);

[name = 'Capital law of motion:C']
K_C*exp(g) = (1-Sc)*I_C*exp(g) + (1-delta_C)*K_C(-1);

[name = 'Capital law of motion:I']
K_I*exp(g) = (1-Si)*I_I*exp(g) + (1-delta_I)*K_I(-1);


[name = 'Labor demand:C']
(1-phi)*W_C = alpha_N*(C+A_C*D_C^phi*nu_C)/N_C;

[name = 'Labor demand:I']
(1-phi)*W_I/p_I = alpha_N*(I+A_I*D_I^phi*nu_I)/N_I;

[name = 'Capital demand:C']
(1-phi)*R_C = exp(g)*alpha_K*(C+A_C*D_C^phi*nu_C)/(h_C*K_C(-1));

[name = 'Capital demand:I']
(1-phi)*R_I/p_I = exp(g)*alpha_K*(I+A_I*D_I^phi*nu_I)/(h_I*K_I(-1));

[name = 'Labor composition']
N = N_C + N_I;

[name = 'Capital composition']
K = K_C + K_I;

[name = 'Investment composition']
I = I_C + I_I;

[name = 'Shopping composition']
D = D_C + exp(theta_I)*D_I;

[name = 'Output (base-year prices)']
Y = C + p_I_ss*I;

[name = 'Capacity utilization']
util_ND = A_C*D_C^phi*((h_C*K_C(-1))^alpha_K*N_C^alpha_N-nu_C)/(K_C(-1)^alpha_K*N_C^alpha_N-nu_C);

util_D = A_I*D_I^phi*((h_I*K_I(-1))^alpha_K*N_I^alpha_N-nu_I)/(K_I(-1)^alpha_K*N_I^alpha_N-nu_I);

util = (C/Y)*util_ND + (I/Y)*util_D;
 
% Exogenous processes
[name='stochastic trend process']
g = (1-rho_g)*g_bar + rho_g*g(-1) + e_g;

[name='Stationary TFP process']
Z_C = rho_Z*Z_C(-1) + e_Z;

[name='Independent component of I-specific tech']
u_ZI = rho_ZI*Z_I(-1) + e_ZI;

[name ='Investment-specific TFP process']
Z_I = Z_C + u_ZI;

[name ='Labor supply process']
theta_N = rho_N*theta_N(-1) - e_N;

[name ='Shopping effort process']
theta_D = rho_D*theta_D(-1) - e_D;

[name ='Relative shopping effort process']
theta_I = rho_DI*theta_I(-1) - e_DI;

[name='Consumption preference process']
theta_b = rho_b*theta_b(-1) + e_b;

[name = 'Wage-markup process: C']
mu_C = rho_muC*mu_C(-1) + e_muC;

[name = 'Wage-markup process: I']
mu_I = rho_muI*mu_I(-1) + e_muI;


[name='Definition log output']
log_Y = log(Y) - steady_state(log(Y));
[name='Definition log consumption']
log_C = log(C) - steady_state(log(C));
[name = 'Definition log investment']
log_I = log(I) - steady_state(log(I));
[name='Definition log hours']
log_N = log(N) - steady_state(log(N));
log_NC = log(N_C) - steady_state(log(N_C));
log_NI = log(N_I) - steady_state(log(N_I));

[name='log capital']
log_K = log(K) - steady_state(log(K));
[name='Definition log labor productivity']
log_Y_N = log_Y - log_N;
[name = 'Definition log shopping effort']
log_D = log(D) - steady_state(log(D));
[name = 'Definition of log relative investment  price']
log_p_I = log(p_I) - steady_state(log_p_I);
[name = 'Definition of log capacity utilization']
log_util = log(util) - steady_state(log(util));
log_util_ND = log(util_ND) - steady_state(log(util_ND));
log_util_D = log(util_D) - steady_state(log(util_D));

[name = 'Definition of log Solow residual']
log_SR = log_Y - (1-labor_share)*log_K(-1) - labor_share*log_N;

% Observation variables: first differences (demeaned) -> link to data in first differences (p. 58 of Pfeifer's Observation Equations)
C_obs = log_C - log_C(-1) + g - g_bar ;
I_obs = log_I - log_I(-1) + g - g_bar ;
Y_obs = log_Y - log_Y(-1) + g - g_bar ;
Y_N_obs = log_Y_N - log_Y_N(-1) + g - g_bar ;
K_obs = log_K - log_K(-1) + g - g_bar;
SR_obs = Y_obs - labor_share*N_obs - (1-labor_share)*K_obs(-1);

% Stationary variables
p_I_obs = log_p_I - log_p_I(-1);
N_obs = log_N - log_N(-1);
NC_obs = log_NC - log_NC(-1);
NI_obs = log_NI - log_NI(-1);
util_ND_obs = log_util_ND - log_util_ND(-1);
util_D_obs = log_util_D - log_util_D(-1);
util_obs = log_util - log_util(-1);
D_obs = log_D - log_D(-1);


end;

steady_state_model;
    //Do Calibration
    //Calibrate the model to N=0.3, annual interest rate, and normalizations
    N = N_ss;
    Y = 1.0;
    p_I = 1.0;
    
    phi_ss = (eta+1)*m/(1+eta*m);

    I = I_Y;
    C = 1-I_Y;
    D = phi_ss^(eta/(1+eta));
    D_C = (1-I_Y)*D;
    D_I = I_Y*D;

    I_C = I*C;
    I_I = I*I_Y;
    
    A_C_ss = Psi/D_C^phi_ss;
    A_I_ss = Psi/D_I^phi_ss;

    //K = K_Y;
    K = K_Y*exp(g_bar);
    K_I = I_Y*K;
    K_C = (1-I_Y)*K;
    N_I = I_Y*N;
    N_C = (1-I_Y)*N;
    N_comp = N;

    nu_C_ss = nu_R*C/Psi;
    nu_I_ss = nu_R*I/Psi;
   

    alpha_N_ss = (1-phi_ss)*labor_share/(1+nu_R);
    W_C = labor_share*Y/N;
    W_I = W_C;

    zeta = C*(1-ha) - D^(1+1/eta)/(1+1/eta);
    theta_N_s = (1-phi_ss)*W_C/(N^(1/nu)*zeta*mu_ss);
    Gam = (C*(1-ha) - D^(1+1/eta)/(1+1/eta) - theta_N_s*N_comp^(1+1/nu)/(1+1/nu)*zeta);
   
    //r_ss = (1+r_ann)^(1/4) - 1.0;
    //beta_ss = (1/(1+r_ss))*exp(g_bar)^(gam);
    //delta_ss = I_Y/K_Y*(1+g_bar) - g_bar;
    r_ss = exp(g_bar)^gam/beta - 1.0;
    delta_ss = I_Y/K_Y + 1 - exp(g_bar);
    sigma_b_ss = r_ss + delta_ss;
    alpha_K_ss = (r_ss+delta_ss)*K_Y/(1+nu_R);

    g = g_bar;

    % Utilization variables
    h_C = 1;
    h_I = 1;
    delta_C = delta_ss;
    delta_I = delta_ss;

    delta_C_pr = sigma_b_ss;
    delta_I_pr = sigma_b_ss;

    Q_C = p_I/(1-phi_ss);
    Q_I = Q_C;
    x_C = exp(g_bar);
    x_I = x_C;
    Sc = 0;
    Si = 0;
    Sc_pr = 0;
    Si_pr = 0;
    

    util_ND = A_C_ss*D_C^phi_ss*((h_C*K_C)^alpha_K_ss*N_C^alpha_N_ss-nu_C_ss)/(K_C^alpha_K_ss*N_C^alpha_N_ss-nu_C_ss);
    util_D = A_I_ss*D_I^phi_ss*((h_I*K_I)^alpha_K_ss*N_I^alpha_N_ss-nu_I_ss)/(K_I^alpha_K_ss*N_I^alpha_N_ss-nu_I_ss);
    util = (C/Y)*util_ND + (I/Y)*util_D;

    Z_C = 0;
    Z_I = 0;
    u_ZI = 0;
    theta_N = 0;
    theta_D = 0;
    theta_b = 0;
    theta_I = 0;
    mu_C = 0;
    mu_I = 0;

   
    R_C = W_C*exp(g)*(alpha_K_ss/alpha_N_ss)*N_C/K_C;
    R_I = R_C;
    
    % Logged versions of variables
    log_Y = 0;
    log_C = 0;
    log_I = 0;
    log_N = 0;
    log_NC = 0;
    log_NI = 0;
    log_K = 0;
    log_Y_N = 0;
    log_D = 0;
    log_p_I = 0;
    log_util = 0;
    log_SR = 0;
    
    C_obs = 0;
    I_obs = 0;
    Y_obs = 0;
    Y_N_obs = 0;
    N_obs = 0;
    NC_obs = 0;
    NI_obs = 0;
    K_obs = 0;
    p_I_obs = 0;
    util_obs = 0;
    util_ND_obs = 0;
    util_D_obs = 0;
    D_obs = 0;
    SR_obs = 0;

end;

//set shock variances
shocks;
    var e_g=0.0072^2;
    var e_Z = 0.0072^2;
    var e_ZI=0.0072^2;
    var e_N = 0.0072^2;
    var e_D = 0.0072^2;
    var e_b = 0.0072^2;
    var e_DI = 0.0072^2;
end;

// local identification
//identification(ar=10);
//check the starting values for the steady state
resid;

// compute steady state given the starting values
steady;
// check Blanchard-Kahn-conditions
check;

%/*
estimated_params;
//x, init_value, lower bound, upper bound, prior shape, prior mean, prior std

//gam, 1.5, 1.0, gam_max,       BETA_PDF, 1.5, 0.25, 1.0, gam_max;
gam, 1.5, 1.0, 4.0,             BETA_PDF, 1.5, 0.25, 1.0, 4.0;
ha, 0.5, 0.0, 0.95,           BETA_PDF, 0.5, 0.2;
nu, 0.72, 0.05, 2.0,           GAMMA_PDF, 0.72, 0.25;
sigma_s, 0.5, 0.0, 1.0,        BETA_PDF, 0.5, 0.2; %Born, Peter, and Pfeifer (2013)
m, 0.286, 0.0, 0.95,          GAMMA_PDF, 0.286, 0.2;

nu_R, 0.20, 0.01, 0.5,        BETA_PDF, 0.2, 0.1;

sigma_ac, 0.32, 0.0, 10,       INV_GAMMA_PDF, 1, 1; % Schmitt-Grohe and Uribe (2010), Katayama and Kim (2018)
sigma_ai, 0.32, 0.0, 10,       INV_GAMMA_PDF, 1, 1; % Schmitt-Grohe and Uribe (2010), Katayama and Kim (2018)
Psi_C, 1.5, 0.0, 50,           GAMMA_PDF, 4, 1.0; % Schmitt-Grohe and Uribe (2010), Katayama and Kim (2018)
Psi_I, 1.5, 0.0, 50,           GAMMA_PDF, 4, 1.0; % Schmitt-Grohe and Uribe (2010), Katayama and Kim (2018)

//phi, 0.32, 0.00, 0.999,        BETA_PDF, 0.32, 0.2;
eta, 0.20, 0.00, 10.0,          GAMMA_PDF, 0.2, 0.15;

theta, 0.5, .00, 10,   GAMMA_PDF, 1, 0.5; %Katayama and Kim 2018, based on Horvath (2000)


% Persistence parameters
rho_g,  0.1, 0.0001, 0.9,        BETA_PDF, 0.1, 0.05;
rho_Z, 0.95, 0.01, 0.999999,     BETA_PDF, 0.6, 0.2;
rho_ZI,  0.95, 0.01, 0.999999,    BETA_PDF, 0.6, 0.2;
rho_N,  0.6, 0.01, 0.9999,        BETA_PDF, 0.6, 0.2;
rho_D,  0.9, 0.01, 0.9999,        BETA_PDF, 0.6, 0.2;
rho_DI, 0.9, 0.01, 0.9999,          BETA_PDF, 0.6, 0.2;
rho_b,  0.95, 0.01, 0.99999999,        BETA_PDF, 0.6, 0.2;
rho_muC,  0.95, 0.01, 0.99999999,        BETA_PDF, 0.6, 0.2;
rho_muI,  0.95, 0.01, 0.99999999,        BETA_PDF, 0.6, 0.2;

% Standard errors
stderr e_g, 0.01, 0.00001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_Z, 0.01, 0.00001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_ZI, 0.01, 0.0001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;

stderr e_N, 0.01, 0.0001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_D, 0.01, 0.0001, 0.4,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_DI, 0.01, 0.0001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_b, 0.01, 0.0001, 0.4,  INV_GAMMA_PDF, 0.01, 0.1;

stderr e_muC, 0.01, 0.0001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;
stderr e_muI, 0.01, 0.0001, 0.2,  INV_GAMMA_PDF, 0.01, 0.1;

end;

options_.TeX=1;

varobs NC_obs, NI_obs, C_obs, I_obs, p_I_obs, util_ND_obs, util_D_obs;


estimation(tex, optim=('MaxIter', 200), 
datafile=observables_sectoral, 
//mode_file=BRS_sectoral_mode, 
//nograph,
//load_mh_file, 
//mh_recover,
mcmc_jumping_covariance=prior_variance,

mode_compute=4,
presample=0, 
lik_init=2,
mh_jscale=0.002, 
mh_init_scale =0.0001,
//mh_jscale=0.1,
mode_check, 
mh_replic=150000, 
//mh_replic=0,
mh_nblocks=2, 
//bayesian_irf,
//irf=100,
mh_drop=0.3, 
//moments_varendo,
prior_trunc=0)
Y_obs, Y_N_obs, I_obs, p_I_obs, C_obs, NC_obs, NI_obs, util_ND_obs, util_D_obs;
//log_Y, log_Y_N, log_I, log_p_I, log_C, log_N, log_NC, log_NI, util;



%----------------------------------------------------------------
% generate LaTeX output
%----------------------------------------------------------------

write_latex_dynamic_model;
write_latex_parameter_table;
write_latex_definitions;
write_latex_prior_table;
//generate_trace_plots(1);
collect_latex_files;
% if system(['pdflatex -halt-on-error -interaction=batchmode ' M_.fname '_TeX_binder.tex'])
%     error('TeX-File did not compile.')
% end

%*/
% Stochastic simulation -> for conditional FEVD and IRF
stoch_simul (order=1, nofunctions, irf=100, periods=0,
conditional_variance_decomposition=[1 4 8 40])
Y_obs, Y_N_obs, SR_obs, I_obs, p_I_obs, C_obs, NC_obs, NI_obs, util_ND_obs, util_D_obs,
log_Y, log_Y_N, log_SR, log_I, log_p_I, log_C, log_N, log_NC, log_NI, log_util_ND, log_util_D;

